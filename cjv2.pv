free c:channel.  (* declaring a free channel name *)

type id.
type symkey. (* symmetric key type *)
type pkey. (* asymmetric public key type *)
type skey. (* asymmetric secret key type *)

free a, b, d:id.


(* Symetric key cryptography *)
fun ltk(id):symkey [private].		(* symmetric key key*)
fun enc(bitstring, symkey):bitstring. 	(* symmetric encryption *)
fun dec(bitstring, symkey):bitstring.	(* symmetric decryption *)
equation forall x:bitstring, y:id; dec(enc(x,ltk(y)),ltk(y)) = x.

(* type converter *)
fun symkey2bitstring(symkey):bitstring [typeConverter].
fun bitstring2symk(bitstring):symkey [typeConverter].
fun id2bitstring(id):bitstring [typeConverter].
fun bitstring2id(bitstring):id [typeConverter].
fun bitstring2symkey(bitstring):symkey [typeConverter].

(* hash function *)
fun hash(bitstring):bitstring.

(* Asymmetric key cryptography *)
fun pk(id):pkey.			(* public key*)
fun sk(id):skey [private]. (* sk is a private function, i.e. cannot be applied by the adversary *)
fun aenc(bitstring, pkey):bitstring. 	(* asymmetric encryption *)
fun adec(bitstring, skey):bitstring.	(* asymmetric decryption *)
equation forall x:bitstring, y:id; adec(aenc(x,pk(y)),sk(y)) = x. 

(* several events*)
event reach.
event startI(id,id,bitstring).
event acceptI(id,id).
event startR(id,id).
event acceptR(id,id,bitstring).

(*query event(reach()).*)
(*query attacker((new na, new nb)).*)
(*query attacker( new q ).*)
query x:bitstring; inj-event(acceptR(a,b,x)) ==> inj-event(startI(a,b,x)). 		(* injective correspondece property to check the authentication property*)


let PA(ia:id, ra:id) =
	new na:bitstring;
	event startI(ia, ra, na);
	out(c, ia); 							(* a --> b: A*)
	in(c, nBkA:bitstring);					(* b --> a: {Nb}pk(A) *)
	let nonceB = adec(nBkA, sk(ia)) in 		(* decrypt message to get Nb *)			
	let nAkB = aenc(na, pk(ra)) in
		out(c, nAkB);						(* a --> b: {Na}pk(B) *)
	in(c, enchNaNbAB:bitstring);				(* b --> a: {h(Na, Nb, A, B)}pk(A) *)
	let hNaNbAB = hash((na, nonceB, ia, ra)) in 
	let hNbNaAB = hash((nonceB, na, ra, ia)) in 
	let enchNbNaBA = aenc(hNbNaAB, pk(ra)) in
	if (hNaNbAB = adec(enchNaNbAB,sk(ia))) then (* check if the message received = h(Na, Nb, A, B) *)
		out(c, enchNbNaBA);						(* a --> b: h(Nb, Na, B, A) *)
	if( not(ia=d) || not(ra=d) ) then  ( 		(*check that the attacker does not communicate with itself. *)
		new q:bitstring;
		out(c, enc(q, ltk( bitstring2id((na, nonceB)) ) ));			(* this line is used to check secrecy of na and nb. In this file, we only check authentication. *)
		event acceptI(ia, ra)					(* event to check authentication *)
	).

let PB(ib:id, rb:id)=
	new nb:bitstring;
	event startR(ib, rb);
	in(c, =ib);											(* a --> b: A*)
	let nBkA = aenc(nb, pk(ib)) in
		out(c, nBkA);									(* b --> a: {Nb}pk(A) *)
	in(c, nAkB:bitstring);								(* a --> b: {Na}pk(B) *)
	let nonceA = adec(nAkB, sk(rb)) in
	let hNaNbAB = hash((nonceA, nb, ib, rb)) in
	let enchNaNbAB = aenc(hNaNbAB, pk(ib)) in 
		out(c, enchNaNbAB);								(* b --> a: {h(Na, Nb, A, B)}pk(A) *)
	in(c, enchNbNaBA:bitstring);						(* a --> b: h(Nb, Na, B, A) *)
	let hNbNaBA = adec(enchNbNaBA, sk(rb)) in 
	if (hNbNaBA = hash( (nb, nonceA, rb, ib) ) 			(* check that the message received is hash(Nb, Na, B, A). *)
		&& ( not(ib=d) || not(rb=d) ) ) then (  		(* check that the attacker does not communicate with itself. *)
		new r:bitstring;											
		out(c, enc(r, ltk( bitstring2id( (nonceA, nb)) ) ));	(* this line is used to check secrecy of na and nb. In this file, we only check authentication. *)
		event acceptR(ib, rb, nb);
		event reach
	).

process ! in(c,i1:id); ! in(c,r1:id);  PA(a, r1) | 	(* main process: the attacker can choose A can connect to whom. The message is replicated by using ! op. *)
		! in(c,i2:id); ! in(c,r2:id);  PB(i2, r2) | (* main process: the attacker can choose B can connect to whom. *)
		out(c,sk(d)); out(c,ltk(d)) (* the attacker leaks its own key for attacking. *)
		
